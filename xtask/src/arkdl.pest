root = { SOI ~ ( stmt )* ~ EOI }

generics = { "<" ~ item ~ ">" }

item = _{ ( item_array | item_generic | item_standard ) }
    item_array = { "[" ~ item ~ "]" }
    item_generic = { ident ~ generics }
    item_standard = { ident }

stmt = { ident ~ ( generics )? ~ "::" ~ ( stmt_enum | stmt_service | stmt_type ) }
    field = { ident ~ ":" ~ item }
    stmt_enum = { "enum" ~ "{" ~ ( ident )* ~ "}" }
    stmt_service = { "service" ~  "{" ~ ( function )* ~ "}" }
        function = { ident ~ "(" ~ ( field ~ "," )* ~ ( field )? ~ ")" ~ ":" ~ item }
    stmt_type = { "type" ~  "{" ~ ( field )* ~ "}" }

ident = @{ (alpha | digit)+ }
    alpha = { 'a'..'z' | 'A'..'Z' }
    digit = { '0'..'9' }

WHITESPACE = _{ " " | "\t" | "\n" }
